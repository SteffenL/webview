include(CTest)
if(BUILD_TESTING)
    set(TIMEOUT_SECONDS 5)

    function(add_tests_from_directory DIR)
        cmake_path(GET DIR FILENAME DIRNAME)
        file(GLOB_RECURSE TEST_SOURCES CONFIGURE_DEPENDS ${DIR}/*_test.c ${DIR}/*_test.cc)
        foreach(TEST_SOURCE ${TEST_SOURCES})
            set(PREFIX ${DIRNAME}_)
            cmake_path(GET TEST_SOURCE STEM TARGET_NAME)
            string(REGEX REPLACE _test$ "" TEST_SHORT_NAME ${TARGET_NAME})
            add_executable(${TARGET_NAME} ${TEST_SOURCE})
            target_include_directories(${TARGET_NAME} PRIVATE include)
            target_link_libraries(${TARGET_NAME} PRIVATE webview_build_support webview_s)
            set(TEST_FULL_NAME ${PREFIX}${TEST_SHORT_NAME})
            add_test(NAME ${TEST_FULL_NAME} COMMAND ${TARGET_NAME})
            set_tests_properties(${TEST_FULL_NAME} PROPERTIES TIMEOUT ${TIMEOUT_SECONDS})
        endforeach()
    endfunction()

    function(add_tests_from_subdirectories DIR)
        cmake_path(CONVERT ${DIR} TO_CMAKE_PATH_LIST DIR NORMALIZE)
        file(GLOB NAMES LIST_DIRECTORIES true CONFIGURE_DEPENDS ${DIR}/*)
        foreach(NAME ${NAMES})
            add_tests_from_directory(${NAME} ${DIR}/${NAME})
        endforeach()
    endfunction()

    add_tests_from_subdirectories(src)
endif()
