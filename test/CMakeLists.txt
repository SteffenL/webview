include(CTest)
if(BUILD_TESTING)
    set(TIMEOUT_SECONDS 5)

    function(add_tests_from_directory DIR APP_TYPE)
        cmake_path(GET DIR FILENAME DIRNAME)
        file(GLOB_RECURSE TEST_SOURCES CONFIGURE_DEPENDS ${DIR}/*_test.c ${DIR}/*_test.cc)
        foreach(TEST_SOURCE ${TEST_SOURCES})
            set(PREFIX ${DIRNAME}_)
            cmake_path(GET TEST_SOURCE STEM TARGET_NAME)
            string(REGEX REPLACE _test$ "" TEST_SHORT_NAME ${TARGET_NAME})
            if(APP_TYPE STREQUAL "GUI")
                add_executable(${TARGET_NAME} WIN32 ${TEST_SOURCE})
            elseif(APP_TYPE STREQUAL "CONSOLE")
                add_executable(${TARGET_NAME} ${TEST_SOURCE})
            else()
                message(FATAL_ERROR "Unknown app type: ${APP_TYPE}")
            endif()
            target_include_directories(${TARGET_NAME} PRIVATE include)
            target_link_libraries(${TARGET_NAME} PRIVATE webview_build_support webview_s)
            set(TEST_FULL_NAME ${PREFIX}${TEST_SHORT_NAME})
            add_test(NAME ${TEST_FULL_NAME} COMMAND ${TARGET_NAME})
            set_tests_properties(${TEST_FULL_NAME} PROPERTIES TIMEOUT ${TIMEOUT_SECONDS})
        endforeach()
    endfunction()

    add_tests_from_directory(src/unit CONSOLE)
    add_tests_from_directory(src/functional CONSOLE)
    add_tests_from_directory(src/functional_gui GUI)
endif()
