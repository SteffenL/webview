include(CTest)
if(BUILD_TESTING)
    function(add_tests_from_directory DIR)
        cmake_path(GET DIR FILENAME DIRNAME)
        file(GLOB_RECURSE TEST_SOURCES CONFIGURE_DEPENDS ${DIR}/*_test.c ${DIR}/*_test.cc)
        foreach(TEST_SOURCE ${TEST_SOURCES})
            set(PREFIX ${DIRNAME}_)
            cmake_path(GET TEST_SOURCE STEM TARGET_NAME)
            string(REGEX REPLACE _test$ "" TEST_SHORT_NAME ${TARGET_NAME})
            add_executable(${TARGET_NAME} ${TEST_SOURCE})
            target_include_directories(${TARGET_NAME} PRIVATE include)
            target_link_libraries(${TARGET_NAME} PRIVATE webview_build_support webview_s)
            set(TEST_FULL_NAME ${PREFIX}${TEST_SHORT_NAME})
            add_test(NAME ${TEST_FULL_NAME} COMMAND ${TARGET_NAME})
            set_tests_properties(${TEST_FULL_NAME} PROPERTIES TIMEOUT ${TIMEOUT_SECONDS})
            if(WIN32 AND USE_WEBVIEW2_SHARED_LIBRARY)
                # Help tests find WebView2Loader.dll
                set_tests_properties(${TEST_FULL_NAME} PROPERTIES ENVIRONMENT_MODIFICATION "PATH=path_list_append:$<TARGET_FILE_DIR:WebView2Loader>")
            endif()
        endforeach()
    endfunction()

    function(add_tests_from_subdirectories DIR IGNORE_MISSING)
        cmake_path(CONVERT ${DIR} TO_CMAKE_PATH_LIST DIR NORMALIZE)
        file(GLOB NAMES LIST_DIRECTORIES true CONFIGURE_DEPENDS ${DIR}/*)
        list(LENGTH NAMES NAMES_LENGTH)
        if(NAMES_LENGTH GREATER 0)
            foreach(NAME ${NAMES})
                add_tests_from_directory(${NAME} ${DIR}/${NAME})
            endforeach()
        elseif(NOT IGNORE_MISSING)
            message(FATAL_ERROR "No test subdirectories found in ${DIR}")
        endif()
    endfunction()

    function(add_tests)
        set(TIMEOUT_SECONDS 10)
        set(OS ${CMAKE_SYSTEM_NAME})
        string(TOLOWER ${OS} OS)

        add_tests_from_subdirectories(src/common FALSE)
        add_tests_from_subdirectories(src/${OS} TRUE)
    endfunction()

    add_tests()
endif()
