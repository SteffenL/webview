set(WEBVIEW2_VERSION "1.0.1185.39")
set(TARGET_NAME webview_deps)
add_library(${TARGET_NAME} INTERFACE)

if(WIN32)
    option(USE_WEBVIEW2_SHARED_LIBRARY "Use Microsoft WebView2 as a shared library" OFF)
    if(USE_WEBVIEW2_SHARED_LIBRARY)
        set(WEBVIEW2_LIBRARY WebView2Loader)
    else()
        if(NOT MSVC)
            message(WARNING "Linking WebView2 statically is only supported with MSVC")
        endif()
        set(WEBVIEW2_LIBRARY WebView2LoaderStatic)
    endif()
    if(NOT TARGET ${WEBVIEW2_LIBRARY})
        include(${CMAKE_CURRENT_LIST_DIR}/webview2.cmake)
        fetch_webview2(${WEBVIEW2_VERSION})
    endif()
    target_link_libraries(${TARGET_NAME} INTERFACE ${WEBVIEW2_LIBRARY})
    target_compile_definitions(${TARGET_NAME} INTERFACE WEBVIEW_EDGE)
elseif(APPLE)
    find_library(WEBKIT_LIBRARY WebKit REQUIRED)
    target_link_libraries(${TARGET_NAME} INTERFACE ${WEBKIT_LIBRARY})
    target_compile_definitions(${TARGET_NAME} INTERFACE WEBVIEW_COCOA)
elseif(UNIX)
    find_package(PkgConfig REQUIRED)
    pkg_check_modules(GTK REQUIRED gtk+-3.0)
    pkg_check_modules(WEBKIT2GTK REQUIRED webkit2gtk-4.0)
    target_include_directories(${TARGET_NAME} INTERFACE include ${WEBKIT2GTK_INCLUDE_DIRS})
    target_link_libraries(${TARGET_NAME} INTERFACE ${WEBKIT2GTK_LIBRARIES} pthread)
    target_compile_definitions(${TARGET_NAME} INTERFACE WEBVIEW_GTK)
endif()
