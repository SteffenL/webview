set(CPACK_PACKAGE_NAME webview)
set(CPACK_PACKAGE_VENDOR webview organization)
set(CPACK_PACKAGE_VERSION ${PROJECT_VERSION})
set(CPACK_PACKAGE_DESCRIPTION_SUMMARY "A tiny cross-platform webview library for C/C++ to build modern cross-platform GUIs")
set(CPACK_PACKAGE_INSTALL_DIRECTORY "${CPACK_PACKAGE_NAME}")
set(CPACK_PACKAGE_CHECKSUM SHA256)
set(CPACK_RESOURCE_FILE_LICENSE ${PROJECT_SOURCE_DIR}/LICENSE)
set(CPACK_VERBATIM_VARIABLES TRUE)

if(CMAKE_SYSTEM_NAME STREQUAL "Windows")
    set(CPACK_GENERATOR ZIP)
else()
    set(CPACK_GENERATOR TGZ)
endif()

string(TOLOWER "${CMAKE_SYSTEM_NAME}" CMAKE_SYSTEM_NAME_LC)
string(TOLOWER "${CMAKE_SYSTEM_PROCESSOR}" CMAKE_SYSTEM_PROCESSOR_LC)
string(TOLOWER "${CMAKE_CXX_COMPILER_ID}" CMAKE_CXX_COMPILER_ID_LC)

if(MSVC AND MSVC_TOOLSET_VERSION)
    set(COMPILER_VERSION "${MSVC_TOOLSET_VERSION}")
else()
    string(REPLACE "." ";" COMPILER_VERSION_LIST "${CMAKE_CXX_COMPILER_VERSION}")
    list(GET COMPILER_VERSION_LIST 0 COMPILER_VERSION_MAJOR)
    list(GET COMPILER_VERSION_LIST 1 COMPILER_VERSION_MINOR)
    set(COMPILER_VERSION "${COMPILER_VERSION_MAJOR}.${COMPILER_VERSION_MINOR}")
endif()
set(COMPILER "${CMAKE_CXX_COMPILER_ID_LC}${COMPILER_VERSION}")

if(APPLE AND CMAKE_OSX_ARCHITECTURES MATCHES ";")
    set(PACKAGE_NAME_ARCH universal)
else()
    set(PACKAGE_NAME_ARCH ${CMAKE_SYSTEM_PROCESSOR_LC})
endif()

set(WEBVIEW_ARCHIVE_TARGET_FILE_NAME "${CPACK_PACKAGE_NAME}_v${CPACK_PACKAGE_VERSION}-${COMPILER}_${CMAKE_SYSTEM_NAME_LC}_${PACKAGE_NAME_ARCH}")
set(WEBVIEW_ARCHIVE_UNIVERSAL_FILE_NAME "${CPACK_PACKAGE_NAME}_v${CPACK_PACKAGE_VERSION}")

set(CPACK_SOURCE_GENERATOR TGZ)
set(CPACK_SOURCE_PACKAGE_FILE_NAME "${WEBVIEW_ARCHIVE_UNIVERSAL_FILE_NAME}-source")

file(TO_CMAKE_PATH "${PROJECT_BINARY_DIR}" IGNORED_BUILD_DIR)
string(REPLACE "\\" "\\\\" IGNORED_BUILD_DIR "${IGNORED_BUILD_DIR}")
set(CPACK_SOURCE_IGNORE_FILES
    /\\.git/
    /\\.svn/
    /\\.hg/
    "${IGNORED_BUILD_DIR}"
)
message("PROJECT_BINARY_DIR = ${PROJECT_BINARY_DIR}")
message("IGNORED_BUILD_DIR = ${IGNORED_BUILD_DIR}")
message("CPACK_SOURCE_IGNORE_FILES = ${CPACK_SOURCE_IGNORE_FILES}")

set(CPACK_ARCHIVE_COMPONENT_INSTALL ON)
set(CPACK_ARCHIVE_WEBVIEW_RUNTIME_FILE_NAME "${WEBVIEW_ARCHIVE_TARGET_FILE_NAME}-bin")
set(CPACK_ARCHIVE_WEBVIEW_DEVELOPMENT_FILE_NAME "${WEBVIEW_ARCHIVE_TARGET_FILE_NAME}-dev")
set(CPACK_ARCHIVE_WEBVIEW_DEVELOPMENT_API_DOCS_FILE_NAME "${WEBVIEW_ARCHIVE_UNIVERSAL_FILE_NAME}-apidoc")

include(CPack)
